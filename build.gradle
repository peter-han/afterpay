group 'com.phan.codingexercise'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: "jacoco"
apply plugin: 'checkstyle'
apply plugin: 'pmd'
apply plugin: 'findbugs'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.apache.commons:commons-lang3:3.6'
    compile 'org.apache.commons:commons-collections4:4.1'
    compileOnly 'org.projectlombok:lombok:1.16.20'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

// quality check report
jacocoTestReport {
    group = "Reporting"
    reports {
        xml.enabled true
        csv.enabled false
        html.destination "${buildDir}/reports/tests/coverage"
    }
}
checkstyle {
    ignoreFailures = true
    configFile = rootProject.file('config/checkstyle.xml')
}
findbugs {
    ignoreFailures = true
    findbugsTest.enabled = false
}
// pmd : optional, if you need customization
pmd {
    ignoreFailures = true
    ruleSetFiles = files("${rootProject.rootDir}/config/pmd.xml")
}
test {
    testLogging {
        events "passed", "skipped", "failed"
    }
    finalizedBy jacocoTestReport
}
tasks.withType(Checkstyle) {
    reports {
        xml.enabled false
        html.enabled true
    }
}
tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}
tasks.withType(Pmd) {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}
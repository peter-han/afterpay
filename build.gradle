plugins {
    id 'java'
    id 'jacoco'
    id 'checkstyle'
    id 'pmd'
    id 'findbugs'
    id 'maven'

    id 'net.researchgate.release' version '2.8.0'
}

group 'com.phan.codingexercise'
def artifactId = "afterpay-artifact"

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile(
            'org.apache.commons:commons-lang3:3.6',
            'org.apache.commons:commons-collections4:4.1',
    )
    compileOnly 'org.projectlombok:lombok:1.16.20'
    testCompile('junit:junit:4.12')
}

// quality check report
jacocoTestReport {
    group = "Reporting"
    reports {
        xml.enabled true
        csv.enabled false
        html.destination "${buildDir}/reports/tests/coverage"
    }
}
checkstyle {
    ignoreFailures = true
    configFile = rootProject.file('config/checkstyle.xml')
}
findbugs {
    ignoreFailures = true
    findbugsTest.enabled = false
}
// pmd : optional, if you need customization
pmd {
    ignoreFailures = true
    ruleSetFiles = files("${rootProject.rootDir}/config/pmd.xml")
}
test {
    testLogging {
        events "passed", "skipped", "failed"
    }
    finalizedBy jacocoTestReport
}
tasks.withType(Checkstyle) {
    reports {
        xml.enabled false
        html.enabled true
    }
}
tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}
tasks.withType(Pmd) {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "http://localhost:8081/nexus/content/repositories/releases") {
                authentication(userName: "admin", password: "admin123")
            }
            snapshotRepository(url: 'http://localhost:8081/nexus/content/repositories/snapshots') {
                authentication(userName: 'admin', password: 'admin123')
            }

            def getGitHash = { ->
                def stdout = new ByteArrayOutputStream()
                exec {
                    commandLine 'git', 'rev-parse', '--short', 'HEAD'
                    standardOutput = stdout
                }
                return stdout.toString().trim()
            }

            def buildNumber = System.getenv("BUILD_NUMBER") ?: "0"
            pom.version = "$project.version" + ".$getGitHash" + ".$buildNumber"
            pom.artifactId = "$artifactId"
            pom.groupId = "$project.group"
        }
    }
}
